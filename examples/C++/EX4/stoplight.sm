// -*- tab-width: 4; -*-

%{
//
// The contents of this file are subject to the Mozilla Public
// License Version 1.1 (the "License"); you may not use this file
// except in compliance with the License. You may obtain a copy of
// the License at http://www.mozilla.org/MPL/
// 
// Software distributed under the License is distributed on an "AS
// IS" basis, WITHOUT WARRANTY OF ANY KIND, either express or
// implied. See the License for the specific language governing
// rights and limitations under the License.
// 
// The Original Code is State Machine Compiler (SMC).
// 
// The Initial Developer of the Original Code is Charles W. Rapp.
// Portions created by Charles W. Rapp are
// Copyright (C) 2000 - 2003 Charles W. Rapp.
// All Rights Reserved.
// 
// Contributor(s): 
//
// State Machine
//	Stoplight
//
// Description
//  This state machine is recognizes the regular expression wcw(r) or
//  palindromes.
//
// RCS ID
// $Id: stoplight.sm,v 1.6 2009/03/27 09:41:45 cwrapp Exp $
//
// CHANGE LOG
// $Log: stoplight.sm,v $
// Revision 1.6  2009/03/27 09:41:45  cwrapp
// Added F. Perrad changes back in.
//
// Revision 1.5  2005/05/28 13:31:18  cwrapp
// Updated C++ examples.
//
// Revision 1.0  2003/12/14 19:26:52  charlesr
// Initial revision
//

extern int YellowTimer;
extern int NSGreenTimer;
extern int EWGreenTimer;
%}

%start StopMap::NorthSouthGreen
%class Stoplight
%include "stoplightdefs.h"
%header stoplight.h
%package cpp_ex4

%map StopMap
%%
EastWestGreen
    Entry
    {
        SetTimer(EWGreenTimer);
    }
{
    Timeout
        EastWestYellow
        {
            TurnLight(EWLIGHT, YELLOW);
        }
}

EastWestYellow
    Entry
    {
        SetTimer(YellowTimer);
    }
{
    Timeout
        NorthSouthGreen
        {
            TurnLight(EWLIGHT, RED);
            TurnLight(NSLIGHT, GREEN);
        }
}

NorthSouthGreen
    Entry
    {
        SetTimer(NSGreenTimer);
    }
{
    Timeout
        NorthSouthYellow
        {
            TurnLight(NSLIGHT, YELLOW);
        }
}

NorthSouthYellow
    Entry
    {
        SetTimer(YellowTimer);
    }
{
    Timeout
        EastWestGreen
        {
            TurnLight(NSLIGHT, RED);
            TurnLight(EWLIGHT, GREEN);
        }
}
%%
